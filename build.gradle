ext {
    cloudnetVersion = '3.4.0-SNAPSHOT'
    
    cloudnetDriver = "de.dytanic.cloudnet:cloudnet-driver:$cloudnetVersion"
    cloudnet = "de.dytanic.cloudnet:cloudnet:$cloudnetVersion"
    cloudnetBridge = "de.dytanic.cloudnet:cloudnet-bridge:$cloudnetVersion"
    cloudnetWrapper = "de.dytanic.cloudnet:cloudnet-wrapper-jvm:$cloudnetVersion"
    bungee = "net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT"
    luckperms = "net.luckperms:api:5.4"
    npclib = "com.github.juliarn:npc-lib:2.6-RELEASE"
    protocollib = "com.comphenix.protocol:ProtocolLib:4.8.0"
}

allprojects {
    repositories {
		mavenCentral()
		mavenLocal()
	    maven {
        	url = uri('https://repo.papermc.io/repository/maven-public/')
    	}
    	maven {
    	    url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    	    content {
				includeGroup 'org.bukkit'
				includeGroup 'org.spigotmc'    	        
    	    }
    	}  
    	maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
		maven { url = 'https://oss.sonatype.org/content/repositories/central' }
		maven { url = 'https://libraries.minecraft.net'}
		maven { url = 'https://repo.dmulloy2.net/repository/public/' }
		maven { url = 'https://repo.cloudnetservice.eu/repository/releases/' }
		maven { url = 'https://repo.cloudnetservice.eu/repository/snapshots/' }
		maven { url = 'https://jitpack.io'}
		maven { url = 'https://repo.viaversion.com' }


    }
    
    pluginManager.withPlugin('java') {
		java {
    		toolchain.languageVersion.set(JavaLanguageVersion.of(8))
		}
	
		compileJava.options.encoding = 'UTF-8'
		compileTestJava.options.encoding = 'UTF-8'
    }

    pluginManager.withPlugin('java-library') {
        configurations {
            embedApi
        }
        
        dependencies {
            configurations.api.extendsFrom(configurations.embedApi)
        }
        
        jar {
        	dependsOn configurations.embedApi
            from {
                configurations.embedApi.collect { it.isDirectory() ? it : zipTree(it) }
            }
        }
    }
}

